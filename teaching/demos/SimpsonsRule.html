<!DOCTYPE HTML>
<html>
  <head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width">
    <title>Simpson's Rule</title>
    <script src="https://sagecell.sagemath.org/static/jquery.min.js"></script>
    <script src="https://sagecell.sagemath.org/embedded_sagecell.js"></script>
    <script>
$(function () {
    // Make *any* div with class 'compute' a Sage cell
    sagecell.makeSagecell({inputLocation: 'div.compute',
    						template:       sagecell.templates.minimal,
                           evalButtonText: 'Launch the Interactive Applet Now'});
});
    </script>
  </head>
  <body style="width: 1000px;">

  <h1>Simpson's Rule</h1>

<p>An Interactive Applet powered by Sage and MathJax.</p>
<p>(By Kelsey Norman with HTML code from Prof. Gregory V. Bard)</p>

<hr>

<script src="http://ajax.googleapis.com/ajax/libs/jquery/1.3.2/jquery.min.js"></script>
<script src="jquery.jslatex.js"></script>

<script>
$(function () {
    $(".latex").latex();
});
</script>

<div class="latex">
    \textrm{Given a function } f, \textrm{ the integral from } a \textrm{ to } b \textrm{ can be approximated by}
    \\
   \textrm{parabolas over } n/2 \textrm{ intervals of width } 2\Delta x, \textrm{ assuming } n \textrm{ is even. Then }
    \\
    \int_a^b f(x) \, dx \approx \frac{\Delta x}{3} (y_0 + 4y_1 + 2y_2 + 4y_3 + ... + 4y_{n-1} + y_n) 
    \\
    \textrm{where each } y_i = f(x_i) \textrm{ for points } x_i = a + i\Delta x \textrm{ from an evenly-spaced}
    \\
 \textrm{partition of the interval } [a,b].
</div>

<hr> 

<div class="compute">
<script type="text/x-sage">
# INTENSE VISUAL TIME
@interact( layout =[ ['func', 'n', 'lower', 'upper'], ['x_min', 'x_max', 'y_min', 'y_max'] ])
def simpsons (func = input_box( default = x*sin(x/2), label = "f(x) = ", width = 20 ), 
        n = input_box( default = 4, label = "n", width = 5 ), 
        lower = input_box( default = 1, label = "a", width = 5 ), 
        upper = input_box( default = 5, label = "b", width = 5 ), 
        x_min = input_box( default = 0, label = "x min", width = 5 ), 
        x_max = input_box( default = 6, label = "x max", width = 5 ),
        y_min = input_box( default = -2, label = "y min", width = 5 ),
        y_max = input_box( default = 6, label = "y max", width = 5 ) ):
    
    # Check that n is even, otherwise cannot compute
    if not n%2 == 0:
        pretty_print( "n must be even to run Simpson's rule." )
        exit()
        
    else:
        # Set up function and width, graph f
        f(x) = func.function(x)
        width = (upper - lower) / n
        approx = 0
        graph = plot( f, x_min, x_max, ymin = y_min, ymax = y_max, thickness = 2 )
        graph += plot( f, lower, upper, ymin = y_min, ymax = y_max, fill = True, fillcolor = "#ccccff" )
        for j in range(n/2) :
            # Set up the three points of the interval
            x1 = lower + 2*j*width
            x2 = x1 + width
            x3 = x1 + 2*width
            y1 = f(x1)
            y2 = f(x2)
            y3 = f(x3)
            
            approx += (width / 3) * ( y1 + 4*y2 + y3 )
            
            # Visually mark the intervals
            graph += point( (x1, y1), color = "blue", size = 20 ) + point( (x2, y2), color = "blue", size = 20 ) 
            graph += point( (x3, y3), color = "blue", size = 20 ) + line([ (x1, 0), (x1, y1)], color = "blue", linestyle = '--' )
            graph += line([ (x2, 0), (x2, y2)], color = "#8080ff", linestyle = '--' ) + line([ (x3, 0), (x3, y3)], color = "blue", linestyle = '--' )
            
            # Solve for a, b, and c
            quad = matrix([ [x1^2, x1, 1, y1], [x2^2, x2, 1, y2], [x3^2, x3, 1, y3] ])
            solutions = quad.rref()
            a = solutions[0][3]
            b = solutions[1][3]
            c = solutions[2][3]
            # Plot the parabola approximation
            quad_eq = a*x^2 + b*x + c
            x1_height = quad_eq(x=x1)
            x3_height = quad_eq(x=x3)
            graph += plot( quad_eq, x1, x3, ymin = y_min, ymax = y_max, color = 'red', thickness = 2 )
            graph += plot( quad_eq, x_min, x_max, ymin = y_min, ymax = y_max, color = ( 0.75, 0.5, 0.5 ) )
            graph += point( (x1, x1_height ), color = 'red', size = 30 )
            graph += point( (x3, x3_height ), color = 'red', size = 30 )
    
        # Combine plots and show
        graph.show()
        
        text1 = "Simpson's Rule approximates the area as " + str(approx.n()) + "."
        (area, prec) = numerical_integral( f, lower, upper )
        l = 0
        while prec <= 10^-l:
            l += 1
            
        text1 += "\nThe actual area is " + str(area) + ", accurate to " + str(l) + " digits."
        pretty_print( text1 )
</script>
</div>

<hr>
Last modified on July 25th, 2017.
</body>
</html>
